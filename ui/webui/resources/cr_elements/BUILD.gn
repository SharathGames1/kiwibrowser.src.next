# Copyright 2018 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//build/config/chromeos/ui_mode.gni")
import("//third_party/closure_compiler/compile_js.gni")
import("//tools/grit/preprocess_if_expr.gni")
import("//tools/polymer/css_to_wrapper.gni")
import("//tools/polymer/html_to_wrapper.gni")
import("//tools/polymer/polymer.gni")
import("//ui/webui/resources/include_polymer.gni")
import("//ui/webui/resources/tools/generate_grd.gni")
import("//ui/webui/webui_features.gni")
import("./cr_elements.gni")

preprocess_folder = "$root_gen_dir/ui/webui/resources/preprocessed/cr_elements"
if (include_polymer) {
  preprocess_src_manifest = "preprocessed_src_manifest.json"
  preprocess_gen_manifest = "preprocessed_gen_manifest.json"
}

if (include_polymer) {
  generate_grd("build_grdp") {
    grd_prefix = "cr_elements"
    out_grd = "$target_gen_dir/${grd_prefix}_resources.grdp"
    public_deps = [ ":preprocess" ]
    manifest_files = [
      "$target_gen_dir/$preprocess_src_manifest",
      "$target_gen_dir/$preprocess_gen_manifest",
    ]

    resource_path_prefix = "cr_elements"
  }
}

group("preprocess") {
  public_deps = [
    ":html_wrapper_files_native",
    ":preprocess_ts",
  ]

  if (include_polymer) {
    public_deps += [
      ":css_wrapper_files",
      ":html_wrapper_files",
      ":preprocess_generated",
      ":preprocess_src",
    ]
  }
}

# TS files are passed to a separate target so that they are not listed in the
# |out_manifest|.
preprocess_if_expr("preprocess_ts") {
  in_folder = "."
  out_folder = preprocess_folder
  in_files = web_component_files_native_ts + native_html_files +
             non_web_component_files_ts
  if (include_polymer) {
    in_files += web_component_files_polymer_ts + polymer_html_files +
                icons_html_files + css_files
  }
}

if (include_polymer) {
  preprocess_if_expr("preprocess_src") {
    in_folder = "."
    out_folder = preprocess_folder
    in_files = [
      "cr_scrollable_behavior.js",
      "policy/cr_policy_indicator_behavior.js",
    ]

    if (is_chromeos_ash) {
      in_files += [
        "cr_container_shadow_behavior.js",
        "find_shortcut_behavior.js",
      ]
    }

    out_manifest = "$target_gen_dir/$preprocess_src_manifest"
  }

  preprocess_if_expr("preprocess_generated") {
    deps = [ ":custom_elements" ]
    in_folder = target_gen_dir
    out_folder = preprocess_folder
    out_manifest = "$target_gen_dir/$preprocess_gen_manifest"
    in_files = [
      "icons.m.js",
      "policy/cr_policy_indicator.js",
      "policy/cr_policy_pref_indicator.js",
      "policy/cr_tooltip_icon.js",
      "shared_vars_css.m.js",
    ]

    if (is_chromeos_ash) {
      in_files += [ "chromeos/cros_color_overrides.m.js" ]
    }
  }
}

if (include_polymer) {
  group("closure_compile") {
    deps = [
      # Targets for auto-generated Polymer 3 JS Modules
      ":cr_elements_module_resources",
      "policy:closure_compile_module",
    ]
  }
}

# Targets for auto-generating Polymer 3 JS Modules.
if (include_polymer) {
  polymer_modulizer("shared_vars_css") {
    js_file = "shared_vars_css.m.js"
    html_file = "shared_vars_css.html"
    html_type = "custom-style"
  }

  polymer_modulizer("icons") {
    js_file = "icons.m.js"
    html_file = "icons.html"
    html_type = "iron-iconset"
  }
}

if (include_polymer) {
  group("custom_elements") {
    public_deps = [
      ":icons_module",
      ":shared_vars_css_module",
      "policy:web_components",
    ]

    if (is_chromeos) {
      public_deps += [ "chromeos:polymer3_elements" ]
    }
  }

  js_type_check("cr_elements_module_resources") {
    is_polymer3 = true
    deps = [ ":cr_scrollable_behavior" ]

    if (is_chromeos_ash) {
      deps += [
        ":cr_container_shadow_behavior",
        ":find_shortcut_behavior",
      ]
    }
  }

  if (is_chromeos_ash) {
    js_library("cr_container_shadow_behavior") {
      deps = [ "//ui/webui/resources/js:assert.m" ]
    }
  }

  js_library("cr_scrollable_behavior") {
    deps = [
      "//third_party/polymer/v3_0/components-chromium/iron-list:iron-list",
      "//third_party/polymer/v3_0/components-chromium/polymer:polymer_bundled",
    ]
  }
}

if (is_chromeos_ash) {
  js_library("find_shortcut_behavior") {
    deps = [
      "//ui/webui/resources/js:assert.m",
      "//ui/webui/resources/js:cr.m",
      "//ui/webui/resources/js:util.m",
      "//ui/webui/resources/js/cr/ui:keyboard_shortcut_list",
    ]
  }
}

html_to_wrapper("html_wrapper_files_native") {
  deps = [ ":preprocess_ts" ]
  in_folder = preprocess_folder
  out_folder = preprocess_folder
  in_files = native_html_files
  minify = optimize_webui
  template = "native"
}

if (include_polymer) {
  html_to_wrapper("html_wrapper_files") {
    deps = [ ":preprocess_ts" ]
    in_folder = preprocess_folder
    out_folder = preprocess_folder
    in_files = polymer_html_files + icons_html_files
    minify = optimize_webui
  }

  css_to_wrapper("css_wrapper_files") {
    deps = [ ":preprocess_ts" ]
    in_folder = preprocess_folder
    out_folder = preprocess_folder
    in_files = css_files
    minify = optimize_webui
  }
}
